defines:
  # constants
  TREASURE_GALE_SEEDS: 0x23
  DEV_RING: 0x40

  # wram
  wKeysPressed: 0xc481
  wAnimalRegion: 0xc610
  wInventoryB: 0xc688
  wMapMenu_mode: 0xcbb3
  wMapMenu_cursorIndex: 0xcbb6
  wInventorySubmenu1CursorPos: 0xcbd1
  wRingMenu_mode: 0xcbd3
  wIsLinkedGame: 0xcc01
  wScreenOffsetY: 0xcd08
  w1Link.angle: 0xd009

  # rom 00
  interBankCall: 0x008a
  decHlRef16WithCap: 0x0237

banks:
  0x00:
  # don't play any music. should only be called if the -nomusic flag is
  # given.
  - filterMusic: |
      ld h,a
      cp a,49
      jr nc,.sfx
      ld a,08
      ret
      .sfx
      ld a,(ff00+hMusicVar)
      ret

  # return z iff the current group and room match c and b.
  - compareRoom: |
      ld a,(wActiveGroup)
      cp c
      ret nz
      ld a,(wActiveRoom)
      cp b
      ret

  # searches for a value in a table starting at hl, with an entry matching
  # keys b and subkey c, and values e bytes long. sets c if found. a key of
  # ff ends the table.
  - searchDoubleKey: |
      .loop
      ldi a,(hl)
      cp a,ff
      ret z
      cp b
      jr nz,.next
      ldi a,(hl)
      cp c
      jr nz,.done
      scf
      ret
      .next
      inc hl
      .done
      ld a,e
      rst 10
      jr .loop

  0x02:
  # warp to room under cursor if using tree warp with developer ring.
  - devWarp: |
      ld a,(wActiveRing)
      cp a,DEV_RING
      jr nz,.noWarp
      ld a,(wActiveGroup)
      cp a,02
      jr nc,.noWarp
      or a,80
      ld (wWarpDestGroup),a
      ld a,(wMapMenu_cursorIndex)
      ld (wWarpDestIndex),a
      .noWarp
      ld a,03
      call setMusicVolume
      ret

  # allow ring list to be accessed through the ring box icon.
  - openRingList: |
      ld a,(wInventorySubmenu1CursorPos)
      cp a,0f
      ret nz
      ld a,81
      ld (wRingMenu_mode),a
      ld a,04
      call openMenu
      pop hl
      ret

  # auto-equip rings when selected in ring list.
  - autoEquipRing: |
      call _ringMenu_updateSelectedRingFromList
      ld (wActiveRing),a
      ret

  # don't save gfx when opening ring list from subscreen (they were already
  # saved when opening the item menu), and clear screen scroll variables (which
  # are saved anyway).
  - ringListGfxFix: |
      call setMusicVolume
      ld a,(wRingMenu_mode)
      bit 7,a
      ret z
      and a,7f
      ld (wRingMenu_mode),a
      xor a
      ld (ff00+hCameraY),a
      ld (ff00+hCameraX),a
      ld hl,wScreenOffsetY
      ldi (hl),a
      ldi (hl),a
      jp clearMenu

  # always treat starting seed tree as visited for warping purposes.
  - checkCursorVisited: |
      ld a,(wMapMenu_cursorIndex)
  - checkTreeVisited: |
      cp a,STARTING_TREE_MAP_INDEX
      jp nz,_mapMenu_checkRoomVisited
      or a
      ret

  0x03:
  # allow skipping the capcom screen after one second by pressing start.
  - skipCapcom: |
      push hl
      ld a,(cbb3)
      cp a,94
      jr nc,.noSkip
      call forceEnableIntroInputs
      .noSkip
      pop hl
      jp decHlRef16WithCap

  0x04:
  # look up tiles in custom replacement table after loading a room. the format
  # is (group, room, bitmask, YX, tile ID), with ff ending the table. if the
  # bitmask AND the current room flags is nonzero, the replacement is not made.
  - applyExtraTileSubstitutions: |
      push bc
      push de
      call getThisRoomFlags
      ld e,a
      ld hl,tileSubTable
      ld a,(wActiveGroup)
      ld b,a
      ld a,(wActiveRoom)
      ld c,a
      .loop
      ldi a,(hl)
      cp a,ff
      jr z,.done
      cp b
      jr nz,.groupMismatch
      ldi a,(hl)
      cp c
      jr nz,.roomMismatch
      ldi a,(hl)
      and e
      jr nz,.flagMismatch
      push de
      ld d,cf
      ldi a,(hl)
      ld e,a
      ldi a,(hl)
      ld (de),a
      pop de
      jr .loop
      .groupMismatch
      inc hl
      .roomMismatch
      inc hl
      .flagMismatch
      inc hl
      inc hl
      jr .loop
      .done
      pop de
      pop bc
      call applyAllTileSubstitutions
      ret

  0x05:
  # if wearing dev ring, jump over any tile like a ledge by pressing B with no
  # B item equipped.
  - devJump: |
      push af
      ld a,(wActiveRing)
      cp a,DEV_RING
      jr nz,.next
      ld a,(wInventoryB)
      or a
      jr nz,.next
      ld a,(wKeysPressed)
      and a,02
      jr z,.next
      pop af
      ld a,(w1Link.angle)
      scf
      ret
      .next
      pop af
      ret
