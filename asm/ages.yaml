addrs:
  # hram
  hCameraY: 0xffaa
  hCameraX: 0xffac
  hMusicVar: 0xffb7 # used in filterMusic

  # wram
  wDimitriState: 0xc647
  wAnimalTutorialFlags: 0xc649
  wEssencesObtained: 0xc6bf
  wActiveRing: 0xc6cb
  wRingBoxLevel: 0xc6cc
  wMakuTreeState: 0xc6e8
  wActiveGroup: 0xcc2d
  wActiveRoom: 0xcc30
  wAreaFlags: 0xcc34
  wWarpDestGroup: 0xcc47
  wWarpDestIndex: 0xcc48

  # rom 00
  forceEnableIntroInputs: 0x0886
  playSound: 0x0c98
  setMusicVolume: 0x0cad
  openMenu: 0x1ab0
  setGlobalFlag: 0x31f9

  # rom 02
  _mapMenu_checkCursorRoomVisited: 0x6636
  _mapMenu_checkRoomVisited: 0x6639
  _ringMenu_updateSelectedRingFromList: 0x723b
  clearMenu: 0x50b1

banks:
  0x00:
  # return z iff the current group and room match c and b.
  - compareRoom: |
      ld a,(wActiveGroup)
      cp c
      ret nz
      ld a,(wActiveRoom)
      cp b
      ret

  # read 2 bytes from bank e at hl into bc.
  # TODO: these (ff97)s could be replaced with (ff00+97)s
  - readWord: |
      ld a,(ff97)
      push af
      ld a,e
      ld (ff97),a
      ld (2222),a
      ldi a,(hl)
      ld b,a
      ld a,(hl)
      ld c,a
      pop af
      ld (ff97),a
      ld (2222),a
      ret

  # searches for a value in a table starting at hl, with an entry matching
  # keys b and subkey c, and values e bytes long. sets c if found. a key of
  # ff ends the table.
  - searchDoubleKey: |
      .loop
      ldi a,(hl)
      cp a,ff
      ret z
      cp b
      jr nz,.next
      ldi a,(hl)
      cp c
      jr nz,.next2
      scf
      ret
      .next
      inc hl
      .next2
      ld a,e
      rst 10
      jr .loop

  # searches for an interaction with ID a and returns the ID address in de,
  # and z flag if found.
  - findObjectWithId: |
      push bc
      ld b,a
      ld de,d041
      .loop
      ld a,(de)
      cp b
      jr nz,.next
      pop bc
      ret
      .next
      inc d
      ld a,d
      cp a,e0
      jr c,.loop
      pop bc
      or a
      ret

  # only increment the maku tree's state if on the maku tree screen, or if
  # all essences are obtained, set it to the value it would normally have at
  # that point in the game. this allows getting the maku tree's item as long
  # as you haven't collected all essences.
  - checkMakuState: |
      ld a,(wActiveGroup)
      cp a,02
      jr nc,.notAtMakuTree
      ld a,(wActiveRoom)
      cp a,38
      jr nz,.notAtMakuTree
      ld a,(wMakuTreeState)
      inc a
      cp a,11
      ret
      .notAtMakuTree
      ld a,(wEssencesObtained)
      inc a
      scf
      jr nz,.notAllEssences
      ld a,0e
      ret
      .notAllEssences
      ld a,(wMakuTreeState)
      ret

  0x02:
  # warp to south lynna present tree if holding start when closing the map
  # screen.
  - treeWarp: |
      ld a,(wKeysPressed)
      and a,08
      jr z,.done
      ld a,(wAreaFlags)
      and a,01
      jr nz,.warp
      ld a,5a
      jp playSound
      .warp
      ld hl,wWarpDestGroup
      ld (hl),80
      inc hl
      ld (hl),78
      ld l,4a
      ld (hl),55
      call 5fbf
      .done
      jp 4fba

  # make an exception for the south lynna tree (always visited).
  - checkCursorVisited: |
      ld a,(wMapMenu_cursorIndex)
  - checkTreeVisited: |
      cp a,78
      jp nz,_mapMenu_checkRoomVisited
      or a
      ret

  # display portal popup map icons for bridge builders' screen present and
  # symmetry city past.
  - displayPortalPopups: |
      ld a,(wMapMenu_mode)
      and a
      ld a,(wMapMenu_cursorIndex)
      jr nz,.present
      cp a,25
      jr nz,.ret
      ld a,aa
      jr .gotIcon
      .present
      cp a,13
      jr nz,.ret
      ld a,a3
      .gotIcon
      jp 6255
      .ret
      jp 6248

  0x03:
  # flags in wGlobalFlags to be set at start of game.
  - initialGlobalFlags: |
      db 0a,0c,1d,20,23,2b,33,3d,40,41,43,45,ff

  # set flags to skip opening and a bunch of other things. see
  # doc/technical.md for a dictionary of the flags.
  - setInitialFlags: |
      push hl

      # global flags
      ld hl,initialGlobalFlags
      .loop
      ldi a,(hl)
      cp a,ff
      jr z,.done
      push hl
      call setGlobalFlag
      pop hl
      jr .loop
      .done

      # linked global flags
      ld a,(wIsLinkedGame)
      or a
      jr z,.unlinked
      ld a,38
      push hl
      call setGlobalFlag
      pop hl
      .unlinked

      # animal vars
      ld a,(7fff)
      ld (wAnimalRegion),a
      ld a,03
      ld (wDimitriState),a
      ld a,ff
      ld (wAnimalTutorialFlags),a

      # maku tree state (vanished)
      ld a,01
      ld (wMakuTreeState),a

      # room flags 3 | 6 | 7
      ld a,c8
      ld (c739),a

      # room flag 7
      ld a,80
      ld (c8bb),a

      # room flags 5 | 6
      ld a,60
      ld (c8cb),a

      # room flag 6
      ld a,40
      ld (c77a),a
      ld (c76a),a
      ld (c759),a
      ld (c77c),a
      ld (c72e),a
      ld (c897),a
      ld (c73a),a
      ld (c7ba),a
      ld (c78d),a
      ld (c70a),a
      ld (c9f6),a
      ld (c85c),a
      ld (c883),a
      ld (c80f),a
      ld (c703),a
      ld (c790),a
      ld (c77b),a

      # room flag 3
      ld a,08
      ld (c725),a
      ld (c813),a

      # room flag 0
      ld a,01
      ld (c876),a
      ld (c738),a

      # give L-3 ring box
      ld a,10
      ld (c69f),a
      ld a,03
      ld (wRingBoxLevel),a

      pop hl
      ret
