defines:
  # constants
  BANK_TREASURE_DATA: 0x15
  BANK_OWL_TEXT: 0x3f
  BANK_ROOM_TREASURES: 0x3f
  STARTING_TREE_MAP_INDEX: 0xf8

  # hram
  hBrokenTilePosition: 0x93
  hCameraY: 0xa8
  hCameraX: 0xaa
  hMusicVar: 0xb5 # used in filterMusic

  # wram
  wRickyState: 0xc643
  wAnimalTutorialFlags: 0xc646
  wInventoryB: 0xc680
  wShieldLevel: 0xc6a9
  wSwordLevel: 0xc6ac
  wFluteIcon: 0xc6af
  wObtainedSeasons: 0xc6b0
  wFeatherLevel: 0xc6b4
  wEssencesObtained: 0xc6bb
  wActiveRing: 0xc6c5
  wRingBoxLevel: 0xc6c6
  wRememberedCompanionRoom: 0xcc42
  wActiveGroup: 0xcc49
  wActiveRoom: 0xcc4c
  wRoomStateModifier: 0xcc4e
  wAreaFlags: 0xcc50
  wDungeonIndex: 0xcc55
  wWarpDestGroup: 0xcc63
  wWarpDestIndex: 0xcc64
  wWarpTransition2: 0xcc67
  wDisableTransitions: 0xccab
  wActiveTileType: 0xccb6

  # rom 00
  multiplyABy4: 0x01c3
  getRandomNumber: 0x041a
  copyMemory: 0x0462
  forceEnableIntroInputs: 0x0862
  playSound: 0x0c74
  setMusicVolume: 0x0c89
  giveTreasure: 0x16eb
  checkTreasureObtained: 0x1717
  getRandomRingOfGivenTier: 0x17b9
  refillSeedSatchel: 0x17e5
  showText: 0x184b
  getThisRoomFlags: 0x1956
  openMenu: 0x1a76
  lookupCollisionTable: 0x1ddd
  convertShortToLongPosition: 0x2089
  objectDelete_useActiveObjectType: 0x219e
  objectCopyPosition: 0x21fd
  objectCopyPositionWithOffset: 0x221a
  interactionSetScript: 0x24fe
  createTreasure: 0x271b
  checkGlobalFlag: 0x30c7
  setGlobalFlag: 0x30cd
  setTile: 0x3a52
  getFreeInteractionSlot: 0x3ac6
  interactionDelete: 0x3ad9
  getFreePartSlot: 0x3ea7

  # rom 02
  _closeMenu: 0x4f7b
  clearMenu: 0x5072
  _mapMenu_checkCursorRoomVisited: 0x655d
  _mapMenu_checkRoomVisited: 0x6560
  _ringMenu_updateSelectedRingFromList: 0x716c

  # rom 04
  applyAllTileSubstitutions: 0x5d94

  # rom 05
  _specialObjectGetRelativeTileWithDirectionTable: 0x44aa

  # rom 0b
  scriptEnd: 0x4103

  # rom 15
  treasureObjectData: 0x5129

  # rom 3f
  applyParameter: 0x454e
  realignUnappraisedRings: 0x4675
  interaction60SubidData: 0x66dc

appends:
  0x00:
  # call a function hl in bank 02, preserving af. e can't be used as a
  # parameter to that function, but it can be returned. this function only
  # exists because banks 08 and 09 are so tight on space.
  - callBank2: |
      push af
      ld e,02
      call interBankCall
      pop af
      ret

  # increment hl until (hl) equals either register a or ff. returns z if
  # a match was found.
  - searchValue: |
      push bc
      ld b,a
      .loop
      ldi a,(hl)
      cp b
      jr z,.match
      inc a
      jr z,.noMatch
      jr .loop
      .noMatch
      inc a
      .match
      ld a,b
      pop bc
      ret

  # this is a replacement for giveTreasure that accounts for item progression.
  # call through giveTreasureCustom or giveTreasureCustomSilent, since this
  # function doesn't xor the a that it returns. importantly, this replacement
  # treats c as a subID, not a param, so this should *not* be called by
  # non-randomized whatevers.
  - giveTreasureCustom_body: |
      ld b,a
      push hl
      ld e,BANK_TREASURE_DATA
      ld hl,getTreasureDataBCE
      call interBankCall
      pop hl
      ld a,b
      jp giveTreasure

  # just gives the treasure, no sound or text.
  - giveTreasureCustomSilent: |
      call giveTreasureCustom_body
      xor a
      ret

  # gives the treasure, plays its sound, and shows its text.
  - giveTreasureCustom: |
      call giveTreasureCustom_body
      jr z,.noSound
      push hl
      call playSound
      pop hl
      .noSound
      ld a,e
      cp a,ff
      ret z
      ld b,00
      ld c,e
      call showText
      xor a
      ret

  # override room layout with bank 02's winterLayout when appropriate.
  - loadWinterLayout: |
      push de
      ld a,(wRoomStateModifier)
      cp a,03 # winter
      jr nz,.done
      push bc
      ld bc,9d00
      call compareRoom
      pop bc
      jr nz,.done
      ld a,02 # bank
      ld (ff00+8c),a
      ld hl,winterLayout
      .done
      ld a,(ff00+8c)
      jp 39e2 # can't just ret, idr why

  # check room flags to determine whether to create star ore instead of
  # whatever global flag 0e is. this also fixes a vanilla bug causing star ore
  # to be infinitely rediggable (but only when the first screen is rolled? or
  # that getting it on the first screen doesn't count? something). this is here
  # since there's no room in bank 8.
  - starOreRooms: db 65,66,75,76,ff
  - checkBeachItemObtained: |
      push de
      push hl
      ld de,starOreRooms
      ld h,c8
      .loop
      ld a,(de)
      cp a,ff
      jr z,.done
      ld l,a
      bit 5,(hl)
      jr nz,.done
      inc de
      jr .loop
      .done
      pop hl
      pop de
      ret

  0x01:
  # takes b = high byte of season addr, returns season in b.
  - readDefaultSeason: |
      ld h,7e
      ld l,b
      ld a,(hl)
      ld b,a
      ret

  0x02:
  # warp to ember tree if holding start when closing the map screen.
  - treeWarp: |
      ld a,(wKeysPressed)
      and a,08
      jr z,.done
      ld a,(wAreaFlags)
      and a,AREAFLAG_OUTDOORS
      jr nz,.warp
      ld a,SND_ERROR
      jp playSound
      .warp
      ld hl,cbb7
      ld (hl),05
      xor a
      call 5e7b
      .done
      jp _closeMenu

  # custom room layout for the problematic woods of winter screen in winter.
  # the code here is one 8-tile compression block per line.
  - winterLayout: |
      db 55,80,81,81,81,81
      db 7c,16,80,82,17
      db f0,1b,c4,c4,70,72
      db 00,01,0d,17,c4,80,81,70,71
      db 60,04,70,71,1a,1b,1c,f7
      db 05,80,81,81,70,71,9e,9e
      db 1c,16,04,15,17,80,81
      db 30,1b,99,9b,d9,1a,01,19
      db 00,70,71,15,16,17,f7,7a,8c
      db 11,18,19,80,81,01,19,70

  # skip the cutscene when throwing a bomb into the volcano.
  - skipVolcanoCutscene: |
      call getThisRoomFlags
      set 6,(hl)
      ld de,d244
      ld a,02
      ld (de),a
      ld hl,6314
      call interactionSetScript
      ld a,15
      jp setGlobalFlag

  # star ore and maku tree both need fake treasure ids to check instead of the
  # vanilla ones.
  - makuTreeRooms: db 0b,0c,7b,2b,2c,2d,5b,5c,5d,ff
  - setFakeIdsForStarOreAndMakuTree_body: |
      ld a,(wActiveGroup)
      cp a,01
      jr z,.subrosia
      cp a,02
      jr z,.indoors
      ret
      .subrosia
      ld a,(wActiveRoom)
      ld hl,starOreRooms
      call searchValue
      ret nz
      ld hl,c694
      set 2,(hl)
      ret
      .indoors
      ld a,(wActiveRoom)
      ld hl,makuTreeRooms
      call searchValue
      ret nz
      ld hl,c693
      set 2,(hl)
      ret

  0x04:
  # data for applyAllTileSubstitutions: group,room,flags,yx,tile
  - tileSubTable: |
      db 00,01,01,52,04 # permanently remove flower outside D6 when cut
      db 00,5c,00,64,48 # extend moblin keep railing as chokept for warning
      db 00,5c,00,74,53 # cont.
      db 00,9a,00,14,12 # remove rock across pit blocking exit from D5
      db 00,8a,00,66,64 # ^ but add rock at bottom of cliff to block ricky
      db 00,9a,00,34,04 # remove bush next to rosa portal
      db 00,b0,00,21,13 # remove spool swamp pits to prevent winter softlock
      db 00,b0,00,51,13 # cont.
  - d2AltEntranceTileSubs: |
      # these are enabled only if entrances are randomized
      db fe,8d,00,18,04 # remove left stairs
      db fe,8e,00,12,04 # remove right stairs
  - endTileSubTable: |
      db ff

  # data for checkSetAnimalSavePoint:
  # entered group, entered room, animal room, saved y, saved x.
  - animalSavePointTable: |
      db 04,fa,c2,18,68 # spool swamp cave
      db 05,cc,2a,38,18 # goron mountain east cave
      db 05,b3,8e,58,88 # cave outside d2
      db 04,e1,86,48,68 # cave north of d1
      db 05,c9,2a,38,18 # goron mountain main
      db 05,ba,2f,18,68 # spring banana cave
      db 05,bb,2f,18,68 # cave below spring banana cave
      db 01,05,9a,38,48 # rosa portal
      db 04,39,8d,38,38 # d2 entrance
      db ff

  # if entering certain warps blocked by snow piles, mushrooms, or bushes, set
  # the animal companion to appear right outside the entrance instead of where
  # you left them.
  - checkSetAnimalSavePoint: |
      push bc
      push de
      ld b,a
      ld a,(wWarpDestIndex)
      ld c,a
      ld e,03
      ld hl,animalSavePointTable
      call searchDoubleKey
      jr nz,.done
      ld de,wRememberedCompanionRoom
      ld a,(de)
      cp (hl)
      jr nz,.done
      ld b,02
      inc hl
      inc de
      call copyMemory
      .done
      ld a,c
      pop de
      pop bc
      ret

  0x05:
  # let link jump down the cliff outside d7, in case of winter sans shovel.
  # also let link jump down the snow cliff added in woods of winter. also call
  # devJump if applicable.
  - cliffLookup: |
      call devJump
      ret c
      push af
      ld a,(wActiveGroup)
      or a
      jr nz,.noJumpPopAf
      ld a,(wActiveRoom)
      cp a,d0
      jr nz,.notD7Entrance
      pop af
      cp a,a8
      jr nz,.noJump
      ld a,08
      scf
      ret
      .notD7Entrance
      cp a,9d
      jr nz,.noJumpPopAf
      pop af
      cp a,99
      jr z,.snowJump
      cp a,9b
      jr nz,.noJump
      .snowJump
      ld a,10
      scf
      ret
      .noJumpPopAf
      pop af
      .noJump
      jp lookupCollisionTable

  # do this so that animals don't immediately stop walking onscreen when called
  # on a bridge, namely the one to/from d1.
  - animalEntryIgnoreBridges: |
      call _specialObjectGetRelativeTileWithDirectionTable
      or a
      ret z
      cp a,1a
      ret z
      cp a,1b
      ret

  # this is called to make moosh unrideable on mt cucco in the case of not
  # having flute in a moosh seed.
  - checkFlute: |
      ld a,0e
      jp checkTreasureObtained

  0x06:
  # unset a room flag when the flower outside D6 is broken. see tileSubTable.
  - checkBreakD6Flower: |
      push af
      push bc
      ld bc,0100
      call compareRoom
      pop bc
      jr nz,.done
      ld a,(ff00+hBrokenTilePosition)
      cp a,52
      jr nz,.done
      push hl
      ld hl,c701
      res 0,(hl)
      pop hl
      .done
      pop af
      jp setTile

  # replace a random item drop with gale seeds 1/4 of the time if the player is
  # out of gale seeds. just to be nice since warping out of one-ways is in
  # logic.
  - dropExtraGalesOnEmpty: |
      ld a,TREASURE_GALE_SEEDS
      call checkTreasureObtained
      jr nc,.done
      ld l,b8
      or (hl)
      jr nz,.done
      call getRandomNumber
      cp a,40
      jr nc,.done
      ld c,08
      .done
      call getFreePartSlot
      ret

  # collect modes starting at 80 index this jump table to determine the actual
  # mode.
  - collectSpecialJumpTable: |
      dw collectDiverRoom
      dw collectPoeSkipRoom
      dw collectMakuTree
      dw collectD4Pool
      dw collectD5Armos

  # master diver's room has chest on the left and reward on the right.
  - collectDiverRoom: |
      ld e,4d
      ld a,(de) # object x position
      cp a,80
      ld e,COLLECT_CHEST
      ret c
      ld e,COLLECT_PICKUP_ALT
      ret

  # bombed wall chest in d7 has an item drop on the left side.
  - collectPoeSkipRoom: |
      ld e,4d
      ld a,(de) # object x position
      cp a,80
      ld e,COLLECT_FALL_KEY
      ret c
      ld e,COLLECT_CHEST_MAP_OR_COMPASS
      ret

  # maku tree item drops at a specific script pos, otherwise use regular mode.
  - collectMakuTree: |
      ld a,(d258) # script position
      cp a,8e
      ld e,COLLECT_FALL
      ret z
      ld e,COLLECT_PICKUP
      ret

  # when the falling item hits the water, it creates a new item interaction.
  # that one should have the mode that requires diving to get the item.
  - collectD4Pool: |
      ld e,54
      ld a,(de) # object z position
      sub a,01
      ld e,COLLECT_FALL_KEY
      ret c
      ld e,COLLECT_DIVE
      ret

  # don't override mode for first three items in the d5 armos room, so that
  # they don't set the "item obtained" room flag.
  - collectD5Armos: |
      ld a,(cfd8)
      cp a,04
      ld e,COLLECT_CHEST
      ret z
      ld e,COLLECT_CHEST_NOFLAG
      ret

  0x07:
  # if wearing dev ring, change season regardless of where link is standing.
  - devChangeSeason: |
      ld a,(wActiveRing)
      cp a,DEV_RING
      ret z
      ld a,(wActiveTileType)
      cp a,08
      ret

  0x08:
  # call giveTreasureCustom instead of giveTreasure in the shop iff the given
  # item is randomized. also set fake treasure id 0f when buying the "flute",
  # so that that id can be checked instead of the flute's to determine whether
  # to stock the item.
  - shopGiveTreasure: |
      push bc
      ld b,a
      ld a,l
      push hl
      ld hl,randomizedShopItemLowAddrs
      call searchValue
      pop hl
      ld a,b
      pop bc
      jr z,.next
      call giveTreasure
      ret
      .next
      call giveTreasureCustom
      ld e,42
      ld a,(de) # subid
      cp a,0d
      ret nz
      ld hl,c693 # in wObtainedTreasureFlags
      set 7,(hl)
      ret

  # these are the low bytes of randomized shop item data addresses.
  - randomizedShopItemLowAddrs: |
      db e9,cf,d3,d9

  # star ore item doesn't normally get a subid. values are replaced at
  # randomization.
  - setStarOreIds: |
      inc l
      ld (hl),TREASURE_STAR_ORE
      inc l
      ld (hl),00
      ret

  0x09:
  # use giveTreasureCustom in the subrosian market when appropriate.
  - marketGiveTreasure: |
      push af
      ld a,l
      cp a,db
      jr z,.custom
      cp a,e3
      jr z,.custom
      cp a,f5 # trade star ore
      jr z,.customFakeId
      pop af
      cp a,TREASURE_RING
      jr nz,.next
      call getRandomRingOfGivenTier
      .next
      call giveTreasure
      ld e,42
      ret
      .customFakeId
      push hl
      ld hl,c694
      set 0,(hl)
      pop hl
      .custom
      pop af
      call giveTreasureCustom
      pop de
      scf
      ret

  # mt. cucco platform cave item is normally created using ring-specific code.
  # values are replaced at randomization.
  - createMtCuccoItem: |
      ld bc,0000
      call createTreasure
      jp 6421

  # see function in bank 02.
  - setFakeIdsForStarOreAndMakuTree: |
      push af
      push hl
      ld hl,setFakeIdsForStarOreAndMakuTree_body
      call callBank2
      pop hl
      pop af
      call giveTreasure
      ret

  # animals called by flute normally veto any nonzero collision value for the
  # purposes of entering a screen, but this allows double-wide bridges (1a and
  # 1b) as well. this specifically fixes the problem of not being able to call
  # an animal on the d1 screen, or on the bridge to the screen to the right.
  # the vertical collision check isn't modified, since bridges only run
  # horizontally.
  - checkFluteCollisions: |
      ld b,01
      ld a,(hl)
      cp a,1a
      jr z,.firstTileMatched
      cp a,1b
      jr z,.firstTileMatched
      or a
      ret nz
      .firstTileMatched
      ld a,l
      add a,b
      ld l,a
      ld a,(hl)
      cp a,1a
      jr z,.secondTileMatched
      cp a,1b
      jr z,.secondTileMatched
      or a
      .secondTileMatched
      ld a,l
      ret nz
      call convertShortToLongPosition
      xor a
      ret

  # remove star ore from inventory when buying the first subrosian market
  # item. this can't go in the gain/lose items table, since the given item
  # doesn't necessarily have a unique ID.
  - tradeStarOre: |
      or a
      jr nz,.next
      push hl
      ld hl,c69a
      res 5,(hl)
      pop hl
      .next
      rst 18
      ldi a,(hl)
      ld c,(hl)
      ret

  # check treasure id 0a to determine whether the maku tree gives its intro
  # speech and item instead of checking essences.
  - makuTreeCheckItem: |
      ld a,0a
      call checkTreasureObtained
      ld a,(wEssencesObtained)
      ret

  # use a non-cutscene screen transition for exiting a dungeon via essence, so
  # that overworld music plays, and set maku tree state.
  - essenceWarp: |
      ld a,81
      ld (wWarpTransition2),a
      ld a,(wEssencesObtained)
      call getNumSetBits
      ld (c6df),a # some maku tree state var? ages-disasm doesn't specify it
      ret

  0x0a:
  # flags in wGlobalFlags to be set at start of game.
  - initialGlobalFlags: |
      db 0a,1c,ff

  # in linked, start with the item that *would* be in the d0 sword chest
  # instead of the sword.
  - giveLinkedStartItem: |
      ld a,(7ffe)
      ld c,a
      ld a,(7ffd)
      ld (c682),a
      call giveTreasureCustomSilent
      xor a
      ld (wInventoryB),a
      ret

  # set flags to skip opening and a bunch of other things. see
  # doc/technical.md for a dictionary of the flags.
  - setInitialFlags: |
      # global flags
      push hl
      ld hl,initialGlobalFlags
      .loop
      ldi a,(hl)
      cp a,ff
      jr z,.done
      push hl
      call setGlobalFlag
      pop hl
      jr .loop
      .done
      pop hl

      # animal vars
      ld a,(7fff)
      ld (wAnimalRegion),a
      ld a,ff
      ld (wAnimalTutorialFlags),a

      # room flags 4 | 6
      ld a,50
      ld (c7a7),a # start

      # room flags 3| 5 | 6 | 7
      ld a,e8
      ld (c79a),a # rosa portal

      # room flags 6 | 7
      ld a,c0
      ld (c798),a # troupe
      ld (c7cb),a # first rosa encounter

      # room flag 6
      ld a,40
      ld (c700),a # d6 entrance
      ld (c71d),a # d4 entrance
      ld (c760),a # d3 entrance
      ld (c78a),a # d5 entrance
      ld (c78d),a # d2 entrance
      ld (c796),a # d1 entrance
      ld (c79b),a # sokra stump
      ld (c7b6),a # impa's house
      ld (c7d0),a # d7 entrance
      ld (c7e9),a # sokra in town
      ld (c800),a # d8 entrance
      ld (c829),a # temple of seasons "gate"
      ld (c82a),a # winter tower

      # room flag 0
      ld a,01
      ld (c701),a # flag determines whether flower/rock tile exists

      # give L-3 ring box
      ld a,10
      ld (c697),a # treasure flag
      ld a,03
      ld (wRingBoxLevel),a

      # linked start item
      ld a,(wIsLinkedGame)
      or a
      call nz,giveLinkedStartItem

      ret

  - removeGashaNutRingText: /include removeGashaNutRingText

  0x0b:
  # custom script command to use on d1 entrance screen: set ccaa to 01 until
  # bit of cfc0 is set. fixes a vanilla bug where dismounting an animal on
  # that screen allowed you to enter without the key.
  - d1EntranceScriptCmd: |
      pop hl
      push bc
      ld bc,9600
      call compareRoom
      pop bc
      ret nz
      ld a,01
      ld (ccaa),a
      xor a
      jp 432d

  - script_warnCliffText: |
      db showtext; dw 0026
      db enableinput
      db scriptend

  - script_warnBushText: |
      db scriptend # impossible since 2.2.0?

  - script_warnKeySkipText: |
      db showtext; dw 0226
      db enableinput
      db scriptend

  - script_checkDisplayWarning: |
      db jumpifmemoryeq; dw wActiveRoom; db d9; dw 4e87 # use vanilla script
      db checkcollidedwithlink_onground
      db asm15; dw checkDisplayWarning
      db checkcfc0_bit0
      db disableinput
      db setcounter1,3c
      db jumptable_memoryaddress; dw cfe0 # value set by warning function
      dw script_warnCliffText
      dw script_warnBushText
      dw script_warnKeySkipText

  # give a fake item id for the master diver to check instead of flippers. the
  # item id and subid at the beginning of this script are changed during
  # randomization.
  - script_diverGiveItem: |
      db giveitem; db TREASURE_FLIPPERS; db 00
      db ormemory; dw c694; db 02 # in wObtainedTreasureFlags
      db retscript

  0x11:
  # mount cucco waterfall/vine screen (add warning)
  - waterfallStaticObjects: |
      db f2
      db 1f,08,68,68
      db 22,0a,20,18
      db fe

  # natzu / woods of winter cliff (add warning)
  - flowerCliffStaticObjects: |
      db f2
      db 9c,00,58,58
      db 22,0a,30,58
      db fe

  # sunken city diving spot (add warning)
  - divingSpotStaticObjects: |
      db f2
      db 1f,0d,68,68
      db 3e,31,18,68
      db 22,0a,64,68
      db fe

  # moblin keep -> sunken city ledge (add warning)
  - moblinKeepStaticObjects: |
      db f2
      db ab,00,40,70
      db 22,0a,58,44
      db f8
      db 2d,00,33
      db fe

  # hss skip room (add warning)
  - hssSkipStaticObjects: |
      db f2
      db 22,0a,88,98
      db f3; dw 5593
      db fe

  0x14:
  # make the rod of seasons interaction behave like a regular item,
  # graphically. otherwise asymmetric wide items can't go there.
  - overrideAnimationId: |
      ld e,41
      ld a,(de) # ID
      cp a,e6
      ret nz
      ld e,42
      ld a,(de) # sub ID
      cp a,02
      ld a,e6
      ret nz
      ld a,60
      ret

  0x15:
  # progressive item upgrade data (old ID, old related var, new ID, new addr)
  - progressiveUpgrades: |
      db 01,02,01; dw 52c5 # mirror shield
      db 05,01,05; dw 52dd # noble sword
      db 05,02,05; dw 52e1 # master sword
      db 06,01,06; dw 52f5 # magic boomerang
      db 13,01,13; dw 5329 # hyper slingshot
      db 17,01,17; dw 5331 # roc's cape
      db 19,01,19; dw 52b9 # satchel upgrade 1
      db 19,02,19; dw 52b9 # satchel upgrade 2 (same deal)
      db ff

  - getUpgradedTreasure: /include getUpgradedTreasure
  - getTreasureData_body: /include getTreasureData_body
  - getTreasureDataBCE: /include getTreasureDataBCE
  - getTreasureDataSprite: /include getTreasureDataSprite
  - modifyTreasure: /include modifyTreasure
  - upgradeTreasure: /include upgradeTreasure

  # hard ore item doesn't normally get a subid. values are replaced at
  # randomization.
  - setHardOreIds: |
      inc l
      ld (hl),52
      inc l
      ld (hl),00
      ret

  # set flags that are normally set during the pirate cutscene when skipping
  # it. the season value should be set to the western coast default at
  # randomization.
  - seasonAfterPirateCutscene: db 00
  - setPirateCutsceneFlags: |
      call setGlobalFlag
      ld a,17
      call setGlobalFlag
      ld a,1b
      call setGlobalFlag
      ld hl,c7e2
      set 6,(hl) # remove ship from desert
      ld a,(seasonAfterPirateCutscene)
      ld (wRoomStateModifier),a
      ret

  # ORs the default season in the given area (low byte b in bank 1) with the
  # seasons the rod has (c), then ANDs and compares the results with d.
  - checkSeasonAccessInArea: |
      ld e,01
      ld hl,readDefaultSeason
      call interBankCall
      ld a,b
      or a
      ld a,01
      jr z,.next
      .loop
      sla a
      dec b
      jr nz,.loop
      .next
      or c
      and d
      cp d
      ret

  # returns c if the player has gale seeds and the seed satchel. used for
  # warnings for cliffs and diving.
  - checkGaleSatchel: |
      push bc
      ld b,a
      ld a,TREASURE_SEED_SATCHEL
      call checkTreasureObtained
      jr nc,.next
      ld a,TREASURE_GALE_SEEDS
      call checkTreasureObtained
      .next
      ld a,b
      pop bc
      ret

  # all other warning functions jump here.
  - warnGeneric: |
      call getFreeInteractionSlot
      ret nz
      ld (hl),9f
      ld l,46
      ld (hl),3c
      ld bc,f100
      ld de,w1Link.yh
      call objectCopyPositionWithOffset
      ld a,SND_CLINK
      call playSound
      ld hl,cfc0
      set 0,(hl)
      ret

  # set cfe0 to the cliff warning text, then display the warning.
  - warnCliff: |
      xor a
      ld (cfe0),a
      jp warnGeneric

  # warning for the ledge from natzu to eastern suburbs.
  - warnFlowerCliff: |
      call checkGaleSatchel
      ret c
      ld b,61 # eastern suburbs
      ld d,01 # spring
      call checkSeasonAccessInArea
      ret z
      jp warnCliff

  # warning for the diving spot from sunken city to woods of winter.
  - warnDivingSpot: |
      ld a,b
      cp a,03 # winter
      ret z
      call checkGaleSatchel
      ret c
      ld b,61 # eastern suburbs
      ld d,09 # spring + winter
      call checkSeasonAccessInArea
      ret z
      jp warnCliff

  # warning for the ledge from mt. cucco to sunken city.
  - warnWaterfallCliff: |
      call checkGaleSatchel
      ret c
      ld b,65 # sunken city / mt. cucco
      ld d,02 # summer
      call checkSeasonAccessInArea
      ret z
      jp warnCliff

  # warning for the ledge from moblin keep to sunken city.
  - warnMoblinKeep: |
      call checkGaleSatchel
      ret c
      ld a,(wAnimalRegion)
      cp a,0c # dimitri
      ret nz
      ld a,TREASURE_FEATHER
      call checkTreasureObtained
      ret c
      jp warnCliff

  # warning for small key softlock with HSS skip. checks and sets room flags so
  # as not to display the warning more than once, ever.
  - warnHssSkip: |
      ld a,(ca86) # check if ice puzzle room is visited?
      or a
      ret nz
      call getThisRoomFlags
      bit 6,(hl)
      ret nz
      set 6,(hl)
      ld a,02
      ld (cfe0),a # warning text jump table index
      jp warnGeneric

  # wraps checkDisplayWarning.
  - checkDisplayWarning: |
      push bc
      push de
      call checkDisplayWarning_body
      pop de
      pop bc
      ret

  # this communicates with the warning script by setting bit zero of $cfc0
  # if the warning needs to be displayed (based on room, season, etc), and
  # also displays the exclamation mark if so.
  - checkDisplayWarning_body: |
      ld a,(wRoomStateModifier)
      ld b,a
      ld a,(wObtainedSeasons)
      ld c,a
      ld a,(wActiveRoom)
      cp a,7c
      jp z,warnFlowerCliff
      cp a,6e
      jp z,warnDivingSpot
      cp a,3d
      jp z,warnWaterfallCliff
      cp a,5c
      jp z,warnMoblinKeep
      cp a,78
      jp z,warnHssSkip
      jp warnGeneric

  0x3f:
  # have seed satchel inherently refill all seeds.
  - satchelRefillSeeds: |
      push bc
      call giveTreasure_body
      ld a,b
      pop bc
      push af
      ld a,b
      cp a,TREASURE_SEED_SATCHEL
      jr nz,.notSatchel
      push bc
      push de
      call refillSeedSatchel
      pop de
      pop bc
      .notSatchel
      pop af
      ld b,a
      ret

  # format (ID, subID, jump address). these functions *must* pop af as the last
  # action before returning.
  - customSpriteJumpTable: |
      db 47,00; dw setMembersShop1Sprite
      db 47,02; dw setMembersShop2Sprite
      db 47,05; dw setMembersShop3Sprite
      db 47,0d; dw setShopSprite
      db 59,00; dw setPedestalSprite
      db 60,16; dw setFeatherSprite
      db 6e,00; dw setStolenFeatherSprite
      db 81,00; dw setMarket1Sprite
      db 81,04; dw setMarket2Sprite
      db 81,0d; dw setMarket5Sprite
      db c6,00; dw setHerosCaveSprite
      db e6,02; dw setTempleOfSeasonsSprite
      db ff

  - setMembersShop1Sprite: |
      ld e,08
      ld hl,4cce
      jp setGenericSprite

  - setMembersShop2Sprite: |
      ld e,08
      ld hl,4cd2
      jp setGenericSprite

  - setMembersShop3Sprite: |
      ld e,08
      ld hl,4cd8
      jp setGenericSprite

  - setShopSprite: |
      ld e,08
      ld hl,4ce8
      jp setGenericSprite

  - setPedestalSprite: |
      ld e,0b
      ld hl,6418
      jp setGenericSprite

  - setFeatherSprite: |
      ld e,43
      ld a,(de) # treasure sub ID
      cp a,02
      jp z,setStolenFeatherSprite
      ld hl,671e
      ld a,(wFeatherLevel)
      or a
      jr z,.feather
      ld hl,6721
      .feather
      pop af
      ret

  - setStolenFeatherSprite: |
      ld hl,671e
      ld a,(wFeatherLevel)
      cp a,02
      jr nz,.next
      ld hl,6721
      .next
      pop af
      ret

  - setMarket1Sprite: |
      ld e,09
      ld hl,77da
      jp setGenericSprite

  - setMarket2Sprite: |
      ld e,09
      ld hl,77e2
      jp setGenericSprite

  - setMarket5Sprite: |
      ld e,09
      ld hl,77f4
      jp setGenericSprite

  - setGenericSprite: |
      call lookupItemSprite
      pop af
      ret

  # TODO: if everything is fine then readByte could be changed to something
  # like readBytesWithGap, which just skips a byte? it would make these funcs
  # cleaner and reduce code duplication.

  # ID and subID not adjacent.
  - setHerosCaveSprite: |
      ld e,0a
      ld hl,7b90
      push bc
      call readByte
      ld b,e
      inc hl
      inc hl
      ld e,0a
      call readByte
      ld c,e
      call lookupItemSprite_body
      pop bc
      pop af
      ret

  # ID and subID not adjacent, needs flags changed, can't do wide
  # non-symmetrical items.
  - setTempleOfSeasonsSprite: |
      ld e,15
      ld hl,70ce
      push bc
      call readByte
      ld b,e
      dec hl
      dec hl
      ld e,15
      call readByte
      ld c,e
      call lookupItemSprite_body
      pop bc
      pop af
      ret

  # don't play item fanfare for the item given at the start of a linked game.
  # this doesn't actually check for linked, just for the room.
  - playSoundExceptForLinkedStartItem: |
      ld e,a
      push bc
      push de
      ld bc,a700
      call compareRoom
      pop de
      pop bc
      ld a,e
      ret z
      jp playSound

  # setting a flute's icon and song when obtained. also makes the corresponding
  # animal companion rideable, etc.
  - activateFlute: |
      push af
      push de
      push hl
      ld a,b
      cp a,TREASURE_FLUTE
      jr nz,.done
      ld e,af
      ld a,c
      sub a,0a # get animal index item parameter
      ld (de),a
      add a,42
      ld h,c6
      ld l,a # hl = flags for relevant animal
      cp a,45
      jr nz,.moosh
      set 5,(hl)
      jr .done
      .moosh
      set 7,(hl)
      .done
      pop hl
      pop de
      pop af
      call applyParameter
      ret

replaces:
  0x00:
  - addr: 0x0c76
    old: ld h,a; ld a,(ff00+hMusicVar)
    new: call filterMusic
  - addr: 0x16f6
    old: call giveTreasure_body
    new: call satchelRefillSeeds
  - addr: 0x25d9
    old: ld e,41; ld a,(de)
    new: call overrideAnimationId
  - addr: 0x2600
    old: ld e,41; ld a,(de)
    new: call overrideAnimationId
  - addr: 0x3854
    old: call applyAllTileSubstitutions
    new: call applyExtraTileSubstitutions
  - addr: 0x39df
    old: push de; ld a,(ff00+8c)
    new: jp loadWinterLayout

  # blaino normally unequips rings by setting bit 6, which turns the friendship
  # ring into the dev ring. don't do that.
  - addr: 0x2376
    old: set 6,(hl)
    new: ld (hl),ff

  0x01:
  # remove hard-coded d5 boss key room beep - it beeps just fine based on
  # normal dungeon room properties.
  - addr: 0x4a09
    old: jr z,0c
    new: nop; nop

  0x02:
  # tree warp / map stuff
  - addr: 0x5e9a
    old: call setMusicVolume
    new: call devWarp
  - addr: 0x5ec8
    old: call _mapMenu_checkRoomVisited
    new: call checkTreeVisited
  - addr: 0x602c
    old: jp nz,_closeMenu
    new: call nz,treeWarp
  - addr: 0x6089
    old: jp nz,_closeMenu
    new: call nz,treeWarp
  - addr: 0x609b
    old: call _mapMenu_checkCursorRoomVisited
    new: call checkCursorVisited
  - addr: 0x6578
    old: ld a,(hl); bit 4,a
    new: call devRingCheckRoomVisited
  - addr: 0x65e1
    old: call _mapMenu_checkRoomVisited
    new: call checkTreeVisited

  0x03:
  - addr: 0x4d6b
    old: call decHlRef16WithCap
    new: call skipCapcom

  0x04:
  - addr: 0x461e
    old: ld a,(wWarpDestIndex)
    new: call checkSetAnimalSavePoint

  0x05:
  - addr: 0x493b
    old: call _specialObjectGetRelativeTileWithDirectionTable; or a
    new: call animalEntryIgnoreBridges; nop
  - addr: 0x5fe8
    old: call lookupCollisionTable
    new: call cliffLookup
  - addr: 0x71ea
    old: call _specialObjectGetRelativeTileWithDirectionTable; or a
    new: call animalEntryIgnoreBridges; nop
  - addr: 0x776b
    old: ld a,(wAnimalRegion)
    new: call checkFlute
  - addr: 0x7a65
    old: ld a,(wAnimalRegion)
    new: call checkFlute

  # vanilla game doesn't save non-natzu animal positions when it thinks you
  # won't need them anymore. stop that.
  - addr: 0x45c9
    old: jr z,28
    new: jr 28

  0x06:
  - addr: 0x4774
    old: call setTile
    new: call checkBreakD6Flower
  - addr: 0x47f5
    old: call getFreePartSlot
    new: call dropExtraGalesOnEmpty

  # use expert's or fist ring with only one button unequipped
  - addr: 0x490e
    old: ret nz
    new: nop

  0x07:
  # hooks
  - addr: 0x5b75
    old: ld a,(wActiveTileType); cp a,08
    new: call devChangeSeason; nop; nop

  # drop link below bushes at game start, not above
  - addr: 0x4197
    old: db 38
    new: db 58
  # start linked games without sword
  - addr: 0x41ac
    old: db 82,05,92,24
    new: db a2,10,92,04

  0x08:
  # hooks
  - addr: 0x4bfb
    old: call giveTreasure
    new: call shopGiveTreasure
  - addr: 0x5663
    old: dw 4e87
    new: dw script_checkDisplayWarning
  - addr: 0x62a7
    old: call checkGlobalFlag
    new: call checkBeachItemObtained
  - addr: 0x62f2
    old: inc l; ld (hl),45
    new: call setStarOreIds

  # have horon village shop stock and sell its items from the start, and don't
  # stop the flute appearing because of animal flags.
  - addr: 0x48d7 # sword check
    old: jr nc,34
    new: nop; nop
  - addr: 0x4adf # sword check
    old: jp nc,4b6d
    new: nop; nop; nop
  - addr: 0x4afd # animal + essence + linked checks
    old: jr nz,10
    new: jr 0c

  # check fake treasure id for stocking shop item 3.
  - addr: 0x4a89
    old: ld a,TREASURE_FLUTE
    new: ld a,TREASURE_SHOOTER
  - addr: 0x4af1
    old: ld a,TREASURE_FLUTE
    new: ld a,TREASURE_SHOOTER

  # don't refill seeds when getting the first member's shop item.
  - addr: 0x4c02
    old: call z,refillSeedSatchel
    new: nop; nop; nop

  # remove generic text from shops so that replacement text can be displayed.
  - addr: 0x4d46
    old: db 46,4c,4b,1f,4d,6c,4b,6d,4b,6d,4b,32,00,3b,4b,54,54
    new: db 00,4c,00,1f,4d,00,4b,6d,4b,6d,4b,32,00,00,4b,00,00

  # ignore essence check from master diver ??
  - addr: 0x5886
    old: ld a,TREASURE_ESSENCE
    new: ld a,TREASURE_PUNCH

  # check fake treasure id for digging subrosia seaside.
  - addr: 0x62fd
    old: ld a,TREASURE_STAR_ORE
    new: ld a,12

  # ignore essence check from piratian captain.
  - addr: 0x6c31
    old: cp a,20
    new: cp a,00

  # allow desert pits to work even if player has the actual bell already.
  - addr: 0x73a2
    old: jr c,09
    new: nop; nop

  # allow volcano event without essences
  - addr: 0x7c40
    old: cp a,07
    new: cp a,00
  - addr: 0x7cd2
    old: cp a,07
    new: cp a,00

  # enable exit from volcano room after skipping cutscene.
  - addr: 0x7cf5
    old: ld (wDisableTransitions),a
    new: nop; nop; nop

  # set up for and call skipVolcanoCutscene.
  - addr: 0x7d07
    old: |
      ld a,(wScreenShakeCounterY)
      or a
      ret nz
      call getThisRoomFlags
      set 6,(hl)
      ld a,0b
      ld (wCutsceneTrigger),a
    new: |
      ld a,(d244)
      cp a,01
      ret nz
      call interactionDelete
      ld hl,skipVolcanoCutscene
      jp callBank2

  # ignore sword level for lost woods pedestal.
  - addr: 0x7e62
    old: cp a,03
    new: ld a,01

  0x09:
  - addr: 0x42e0
    old: call giveTreasure
    new: call setFakeIdsForStarOreAndMakuTree
  - addr: 0x4b4f
    old: ld (wWarpTransition2),a
    new: call essenceWarp
  - addr: 0x4d9a
    old: call 4ed9
    new: call checkFluteCollisions
  - addr: 0x4dad
    old: call 4ed9
    new: call checkFluteCollisions
  - addr: 0x641a
    old: ld bc,2701
    new: jp createMtCuccoItem
  - addr: 0x7887
    old: rst 18; ldi a,(hl); ld c,(hl)
    new: call tradeStarOre
  - addr: 0x788a
    old: |
      cp a,2d
      jr nz,03
      call getRandomRingOfGivenTier
    new: |
      call marketGiveTreasure
      jr c,12
      jr 05
  - addr: 0x7d95
    old: call checkTreasureObtained
    new: call makuTreeCheckItem

  0x0a:
  - addr: 0x66ed
    old: db 1e,78,1a,cb,7f,20 # dunno what this is
    new: call setInitialFlags; jp objectDelete_useActiveObjectType
  - addr: 0x7b93
    old: call giveTreasure
    new: call giveTreasureCustom
  - addr: 0x7b9e
    old: jp showText
    new: ret; nop; nop

  0x0b:
  - addr: 0x4b8f
    old: db spawnitem,TREASURE_HEART_CONTAINER,00
    new: db asm15; dw spawnBossItem
  - addr: 0x4bb1
    old: db spawnitem,TREASURE_HEART_CONTAINER,00
    new: db asm15; dw spawnBossItem
  - addr: 0x730d
    old: db giveitem,TREASURE_FLIPPERS; db scriptend
    new: db callscript; dw script_diverGiveItem

  # new script command address and id
  - addr: 0x406d
    old: dw scriptEnd
    new: dw d1EntranceScriptCmd
  - addr: 0x4dea
    old: db checkcfc0_bit0
    new: db b2

  # skip forced ring appraisal and ring list with vasu (prevents softlock).
  - addr: 0x4a2b
    old: db showtext,33
    new: dw 394a # dunno what this is either

  0x11:
  # these are all for adding warning interactions
  - addr: 0x650b
    old: db f2,ab,00,40
    new: db f3; dw moblinKeepStaticObjects; db ff
  - addr: 0x6568
    old: db f2,9c,00,58
    new: db f3; dw flowerCliffStaticObjects; db ff
  - addr: 0x69cc
    old: db f2,1f,0d,68
    new: db f3; dw divingSpotStaticObjects; db ff
  - addr: 0x6c10
    old: db f2,1f,08,68
    new: db f3; dw waterfallStaticObjects; db ff
  - addr: 0x7ada
    old: db f3; dw 5593
    new: db f3; dw hssSkipStaticObjects

  0x15:
  - addr: 0x465a
    old: ld b,a; swap a
    new: call modifyTreasure
  - addr: 0x5a0e
    old: call setGlobalFlag
    new: call setPirateCutsceneFlags
  - addr: 0x5b83
    old: inc l; ld (hl),52
    new: call setHardOreIds

  # skip "you got all four seasons" text from season spirts.
  - addr: 0x57c2
    old: cp a,04
    new: cp a,05

  # rod cutscene
  - addr: 0x70cf
    old: call giveTreasure
    new: call giveTreasureCustom

  0x1f:
  # replace ring appraisal text with "you got the {ring}".
  - addr: 0x5d99
    old: db 03,13,20,49,04,06
    new: db 02,03,0f,fd,21,00

  0x3f:
  - addr: 0x4356
    old: call _interactionGetData
    new: call checkLoadCustomSprite
  - addr: 0x452b
    old: call applyParameter
    new: call activateFlute
  - addr: 0x4535
    old: call playSound
    new: call playSoundExceptForLinkedStartItem
  - addr: 0x460d
    old: ld hl,4616
    new: ld hl,seedCapacityTable
  - addr: 0x461a
    old: set 6,c; call realignUnappraisedRings
    new: nop; jp autoAppraiseRing
  - addr: 0x4fd9
    old: ld (w7ActiveBank),a
    new: call useOwlText
