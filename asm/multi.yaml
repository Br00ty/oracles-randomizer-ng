common:
  00/multiPlayerNumber: db 00

seasons:
  # if the item buffer is nonzero, spawn the item at link and reset the buffer.
  # var INTERAC_MULTI_BYTE is used to signal the destination player number.
  05/checkNetItemBuffer: |
      push bc
      push de
      push hl
      ld hl,wNetTreasureIn
      ldi a,(hl)
      and a
      jr z,.done
      ld b,a
      ld c,(hl)
      ld e,BANK_TREASURE_DATA
      ld hl,reverseTreasureLookup
      call interBankCall
      call createTreasure
      jr nz,.done
      ld de,w1Link.yh
      call objectCopyPosition_rawAddress
      ld a,(multiPlayerNumber)
      ld l,INTERAC_MULTI_BYTE
      ld (hl),a
      ld hl,wNetCountIn
      inc (hl)
      ld hl,wNetCollect
      ld (hl),02
      ld hl,wNetTreasureIn
      xor a
      ldi (hl),a
      ld (hl),a
      .done
      pop hl
      pop de
      pop bc
      call 4226
      ret
  05/5495/: call checkNetItemBuffer

  # returns b = id, c = subid for b = id, c = param.
  # might loop forever if given bad data.
  15/reverseTreasureLookup: |
      ld hl,treasureObjectData
      ld a,b
      rst 18
      rst 18
      .idLoop
      bit 7,(hl)
      jr z,.idDone
      inc hl
      ldi a,(hl)
      ld h,(hl)
      ld l,a
      jr .idLoop
      .idDone
      ld a,c
      ld c,00
      inc hl
      .paramLoop
      cp (hl)
      jr z,.paramDone
      inc hl
      inc hl
      inc hl
      inc hl
      inc c
      jr .paramLoop
      .paramDone
      ret

  # use different text if the item you receive is for another player.
  15/subMultiworldText: |
      call getMultiworldItemDest
      jr z,.local
      call setTextNumberSubstitution
      inc hl
      ld a,TX_REMOTE_ITEM
      jr .done
      .local
      ldi a,(hl)
      .done
      inc e
      ld (de),a
      ret
  15/466f/: call subMultiworldText

  # sets z if the flag is local, and returns the destination player in a.
  15/getMultiworldItemDest: |
      push de
      ld e,INTERAC_MULTI_BYTE
      ld a,(de)
      and a
      ld e,a
      ld a,(multiPlayerNumber)
      jr z,.done
      cp e
      ld a,e
      .done
      pop de
      ret

  # conerts a to BCD and loads it into wTextNumberSubstitution.
  15/setTextNumberSubstitution: |
      push hl
      ld hl,wTextNumberSubstitution
      inc hl
      .hundreds
      sub a,64
      jr c,.next
      inc (hl)
      jr .hundreds
      .next
      add a,64
      dec hl
      .tens
      sub a,0a
      jr c,.done
      inc (hl)
      jr .tens
      .done
      add a,0a
      sla (hl)
      sla (hl)
      sla (hl)
      sla (hl)
      add a,(hl)
      ld (hl),a
      pop hl
      ret
